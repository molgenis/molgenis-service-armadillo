plugins {
    id "jacoco"
    id "java"
    id "com.diffplug.spotless" version "6.25.0"
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
}

repositories {
    maven {
        url 'https://repo1.maven.org/maven2'
    }
    mavenCentral()
}

bootJar.enabled = false

dependencies {
    //spring
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'jakarta.validation:jakarta.validation-api:3.1.1'
    implementation 'org.springframework.boot:spring-boot-starter-json'
    implementation 'org.springframework.retry:spring-retry'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security:spring-security-oauth2-resource-server'
    implementation 'org.springframework.security:spring-security-oauth2-client'
    implementation 'org.springframework.security:spring-security-oauth2-jose'

    //other
    implementation 'commons-io:commons-io:2.15.1'
    implementation 'org.rosuda.REngine:REngine:2.1.0'
    implementation 'org.rosuda.REngine:Rserve:1.8.1'
    implementation 'com.google.guava:guava:33.1.0-jre'
    implementation 'com.google.auto.value:auto-value-annotations:1.10.4'
    implementation 'org.apache.commons:commons-text:1.11.0'
    implementation 'org.json:json:20250517'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.3.1'

    //test
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.security:spring-security-core'
    testImplementation 'org.mockito:mockito-inline:5.2.0'

    //annotation
    annotationProcessor "com.google.auto.value:auto-value:1.10.4"
}

test {
    useJUnitPlatform()
    maxParallelForks = Runtime.runtime.availableProcessors() / 2
    failFast true
    testLogging {
        events "passed", "skipped", "failed" //, "standardOut", "standardError"

        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true

        showStandardStreams = false
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
        html.required = false
    }
    dependsOn test
}

spotless {
    java {
        googleJavaFormat('1.17.0')
    }
}

build.dependsOn spotlessApply
