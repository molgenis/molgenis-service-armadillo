plugins {
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id "jacoco"
    id "java"
    id "com.diffplug.spotless" version "6.25.0"
}

repositories {
    mavenLocal()
    maven {
        url = 'https://repo.maven.apache.org/maven2'
    }
    maven {
        url = 'https://obiba.jfrog.io/artifactory/libs-release-local/'
    }
}

bootJar.enabled = false

dependencies {
    //internal
    implementation project(':r')
    implementation project(':ui')

    //spring
    implementation 'com.google.code.gson:gson'
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security:spring-security-oauth2-client'
    implementation 'org.springframework.security:spring-security-oauth2-jose'
    implementation 'org.springframework.security:spring-security-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus:1.12.4'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework:spring-aspects'
    implementation 'org.springframework.retry:spring-retry'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    //test spring
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation "junit:junit"

    //other
    implementation 'org.rosuda.REngine:REngine:2.1.0'
    implementation 'org.rosuda.REngine:Rserve:1.8.1'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.30'
    implementation("io.swagger.core.v3:swagger-core-jakarta:2.2.30")
    implementation 'com.google.auto.value:auto-value-annotations:1.10.4'
    implementation 'org.obiba.datashield:ds4j-core:2.1.0'
    implementation 'org.obiba.datashield:ds4j-r:2.1.0'
    implementation 'com.github.docker-java:docker-java:3.5.0'
    implementation 'com.github.docker-java:docker-java-transport-zerodep:3.5.0'
    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'
    implementation 'org.apache.parquet:parquet-hadoop:1.15.1'
    implementation 'org.apache.hadoop:hadoop-client:3.3.6'
    implementation("org.apache.parquet:parquet-avro:1.15.1")
    implementation("org.apache.parquet:parquet-common:1.15.1")
    implementation("com.opencsv:opencsv:5.10")
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

    //Overrides docker-java's sub-dependency to fix compatibility issues with apple ARM chips
    //https://github.com/docker-java/docker-java/issues/1876 -->
    implementation 'com.kohlschutter.junixsocket:junixsocket-common:2.9.0'
    implementation 'com.kohlschutter.junixsocket:junixsocket-native-common:2.9.0'

    //test outside spring
    testImplementation 'com.c4-soft.springaddons:spring-security-oauth2-test-webmvc-addons:4.5.1'
    testImplementation 'org.mockito:mockito-inline:5.2.0'

    //annotation processor
    annotationProcessor "com.google.auto.value:auto-value:1.10.4"
}

test {
    useJUnitPlatform()
    maxParallelForks = Runtime.runtime.availableProcessors() / 2
    failFast true
    testLogging {
        events "passed", "skipped", "failed" //, "standardOut", "standardError"

        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true

        showStandardStreams = false
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
        html.required = false
    }
    dependsOn test
}

spotless {
    java {
        googleJavaFormat('1.17.0')
    }
}

build.dependsOn spotlessApply
